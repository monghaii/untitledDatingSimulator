title: dialogueDay1
tags:
position: -131,-1324
---
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<Checkpoint GameManager>>

/// variable declarations
<<declare $been_shoved = false>>
<<declare $has_gross_buff = false>>
<<declare $has_health_buff = false>>
<<declare $has_damage_buff = false>>
<<declare $day = 1>>
<<declare $met_otheranimegirl = false>>
<<declare $fps_won = 0>>
<<declare $got_relationship = 0>>
<<declare $lost_fps = 0>>

<<if $day == 1>>
<<SetBackground GameManager black>>
<<playSound AudioManager true mysterious_voice>>
    Mysterious Voice: Welcome to Untitled Dating Simulator...
        -> I know how to play! Just show me the girls already!
            Mysterious Voice: Okay, okay! Damn!
            <<playSound AudioManager false rooster>>
            <<SetBackground GameManager home>>
            ...
            ...
            ...
            <<playSound AudioManager true home>>
            *yawn* I gotta get up for school...I really don't want to.
            But it's almost summer! Just three more days, and I'll be free.
            I can't wait to keep playing Doki Doki Literature Club Plus! Premium Physical Edition again.
            <<jump BeginDay>>
        -> Woah...
            Mysterious Voice: You are playerName, a high school student who just wants to get home without getting into a relationship...
            Mysterious Voice: You must navigate through the day and avoid flirtatious advances...
            Mysterious Voice: The people at this school are persistent and well-trained in combat. Be careful when rejecting them...
            Mysterious Voice: If your behavior is too repulsive, you'll be attacked and forced to fight for your life...
            Mysterious Voice: If you accidentally court someone or happen to die, you will wake up at the beginning of the day...
                -> That doesn't sound like fun.
                    Mysterious Voice: Well, you're the one who signed up for this...
            Mysterious Voice: At any time, you can refer to the controls by pressing the ESC key and clicking the help button...
            Mysterious Voice: Do you understand? You are stuck here until you do... (click dialogue options to continue...)
                -> I understand.
                    Mysterious Voice: thank you for your patronage...
                -> No. Please explain again.
                    Mysterious Voice: ...what don't you understand?? Fine, I'll explain it again...
                    <<jump dialogueDay1>>
            Mysterious Voice: Now.... it's time to wake up......
<<endif>>
<<playSound AudioManager false rooster>>
<<SetBackground GameManager home>>
...
...
...
<<playSound AudioManager true home>>
*yawn* I gotta get up for school...I really don't want to.
But it's almost summer! Just three more days, and I'll be free.
I can't wait to keep playing Doki Doki Literature Club Plus! Premium Physical Edition again.
<<jump BeginDay>>
===

title: dialogueDay2
tags:
position: 89,-1326
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<playSound AudioManager true home>>
<<SetCharacter GameManager me>>
Ah, another day, another dol- I mean chance to ruin my life and get in a relationship...
I can't wait to keep playing Wii Sports Resort again!
I just have to make sure I don't end up on a date, then it'll all be over...
No matter what, I'll fight to stay single!
<<jump Day2>>
===

title: dialogueDay3
tags:
position: 306,-1324
---
<<Checkpoint GameManager>>
<<SetBackground  GameManager black>>
<<SetCharacter GameManager me>>
<<playSound AudioManager true mysterious_voice>>
zzz
zzz
zzz
<<SetCharacter GameManager me>>
OMG, what happened?
...
Mysterious Voice: You're in day 3. I thought you knew how this game works by now.
<<SetBackground GameManager home>>
<<playSound AudioManager true home>>
Oh right.
It's the last day. I'm so close...true freedom is just within reach!
<<jump day3_otheranimegirl>>
===

title: day3_otheranimegirl
tags:
position: 306,-1324
---
<<SetCharacter GameManager me>>
<<SetBackground GameManager courtyard>>
<<playSound AudioManager true classroom>>
<<set $day = 3>>
<<Checkpoint GameManager>>
Ahh, the last day before summer break. It's beautiful. Walking through the courtyard is so peaceful.
Hm...who's there? Uh oh...
<<ForceCheckpoint GameManager day3_otheranimegirl>>
<<SetCharacter GameManager otheranimegirl 0>>
otheranimegirl: PlayerName~! Don't think I've forgotten about the talk we had yesterday...
otheranimegirl: It's the last day of school! Don't you want to have a summer romance?
otheranimegirl: Or are you planning on asking that HARLOT animegirl out?
    -> Please otheranimegirl, just leave me alone...
        otheranimegirl: All I heard was "please". That's so polite of you! You are the sweetest guy ever!
        otheranimegirl: Since you asked so nicely, yes! I will go out with you.
        (I forgot...she has selective hearing. And I was so close to starting summer single...)
        <<jump LOSE_got_relationship_otheranimegirl>>
    -> Why do you say such mean things about animegirl? She's your friend!
        <<SetCharacter GameManager otheranimegirl 3>>
        otheranimegirl: Why are you defending her? She says horrible things about everyone all the time!
        otheranimegirl: I can't be with a man that defends other women. I deserve a boyfriend who is loyal to me and only me!
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: For breaking my heart, I challenge you to a fight to the death!
        <<StartFPS GameManager true>>
    -> Baby go poo poo in pants.
        <<SetCharacter GameManager otheranimegirl 1>>
        <<if $has_gross_buff == true>>
            otheranimegirl: Yeah, I'm sure you did. You do weird things like chug expired milk.
            <<SetCharacter GameManager otheranimegirl 2>>
            otheranimegirl: Everybody knows about that. And I STILL flirt with you! That's how compassionate I am.
        <<endif>>
        <<if $has_gross_buff == false>>
            otheranimegirl: ...I'm not gonna lie, I wasn't expecting that.
            otheranimegirl: I guess the rumors about you being gross are true. I didn't want to believe it, but I'll try to look past it.
        <<endif>>
        <<jump poopoo>>
===

title: poopoo
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Besides! I love to take care of people. Especially boyfriends!
<<if $has_health_buff == true>>
    otheranimegirl: Remember when I gave you those black market performance enhancing drugs to increase your health points?
    otheranimegirl: I'm a very caring person!
<<endif>>
otheranimegirl: So don't think that pooping your pants is gonna deter me!
    -> What about peepee in pants?
        <<SetCharacter GameManager otheranimegirl 3>>
        otheranimegirl: I already told you I don't care!
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: Unless...you're saying that because you know it doesn't bother me?
        otheranimegirl: Are you trying to appeal to my unwarranted sense of superiority for accepting behaviors other girls would be put off by?
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: I see now...you're trying to ask me to be your girlfriend, you're just too shy.
        otheranimegirl: Oh, playerName! Of course I'll date you!
        (I should try to listen to the things they say...this could've been avoided.)
        <<jump LOSE_got_relationship_otheranimegirl>>
    -> I just don't have time to date. I play video games constantly.
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: Oh...
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: I also LOVE video games! What's your favorite game?
            -> Halo Reach Legendary Edition
                otheranimegirl: Oh, you mean my FAVORITE GAME EVER?
                otheranimegirl: What are we even doing here? This is a sign!
                otheranimegirl: I have the local pastor on speed dial. We're eloping NOW.
                (Maybe if I asked around a bit three days ago, I would've known that was her favorite game...)
                (Now I'm doomed...)
                <<jump LOSE_got_relationship_otheranimegirl>>
            -> Minecraft Pocket Edition
                otheranimegirl: I love that game! It's not my favorite, but I love it! I haven't been able to play since my phone broke, though...
                otheranimegirl: Do you play on iOS or Android?
                    -> iOS.
                        otheranimegirl: Don't you see? We're meant for each other. We even have the same phone!
                        (I think she might've mentioned she was an iPhone user yesterday...I should've paid more attention.)
                    -> Android.
                        <<SetCharacter GameManager otheranimegirl 1>>
                        otheranimegirl: ...
                        otheranimegirl: Well, have a nice summer, I guess.
                        <<jump lastDayOfClass>>
            -> Spider Solitaire
                <<SetCharacter GameManager otheranimegirl 1>>
                otheranimegirl: Oh...that's not really what I was imagining when you said you liked video games.
                otheranimegirl: Um...see you next semester, okay? Bye!
                <<jump lastDayOfClass>>
===

title: lastDayOfClass
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
<<SetBackground GameManager classroom>>
<<SetCharacter GameManager me>>
Ugh...finally, its the last class of the day.
I've managed to dodge these vixens so far...I'm so close to finally getting home and starting my summer vacation!
<<SetCharacter GameManager animegirl 2>>
animegirl: PlayerName! I've been looking all over for you!
(She's not even in this class!)
animegirl: I heard you rejected otheranimegirl this morning. That was really mean of you.
    -> She says horrible things about you behind your back.
        <<SetCharacter GameManager animegirl 1>>
        animegirl: ...Wait, really?
            -> Yeah. She constantly drags your name through the mud.
                animegirl: ...
                <<SetCharacter GameManager animegirl 4>>
                animegirl: I'm gonna BODYSLAM HER when I see her after school today.
                animegirl: And then I'll cast a spell on her to make all her teeth fall out.
                    -> I'm so sorry, animegirl. You're awesome. You don't deserve this.
                        <<SetCharacter GameManager animegirl 1>>
                        animegirl: See, usually saying something really nice to a girl in this game would get you the bad ending, but...
                        animegirl: You did a really decent thing by warning me about that snake. So...I'm gonna let you go. Enjoy your summer.
                        (Huh.)
                        <<jump afterSchool>>
                    -> I don't really care about this girl drama.
                        <<SetCharacter GameManager animegirl 1>>
                        animegirl: Okay, that sounded kind of misogynistic, playerName.
                        animegirl: I think you might've forgotten about me being the archetypal bully character.
                        animegirl: As such, I am legally obligated to do this.
                        animegirl shoves you.
                        <<set $been_shoved = true>>
                        <<TakeDamage GameManager 10>>
                        <<SetCharacter GameManager animegirl 3>>
                        animegirl: Now apologize.
                        <<jump apologizeToAnimegirl>>
            -> Nah, I'm just a shit stirrer. Teehee.
                <<SetCharacter GameManager animegirl 1>>
                animegirl: Why are you trying to come between me and my best friend?
                animegirl: Are you jealous?
                    -> Yes, m'lady. I wish to be close bosom friends with you.
                        <<SetCharacter GameManager animegirl 4>>
                        animegirl: UGH!!! Everyone knows I hate weird affectations like that! Stop it!
                        animegirl: Actually, no. This is day 3. You should've known better!!!
                        <<StartFPS GameManager>>
                    -> *start full body sobbing*
                        <<SetCharacter GameManager animegirl 1>>
                        animegirl: Woah. Dial it back.
                        animegirl: Ugh, forget you. You're so weird.
                        <<jump afterSchool>>
                    -> No way!
                        <<SetCharacter GameManager animegirl 2>>
                        animegirl: I don't believe you~!
                        animegirl: I mean, your first thought when I came in the room was "she's not even in this class".
                        animegirl: If you didn't like me, why would you keep track of my schedule?
                        (She can read minds???)
                        animegirl: Sure can! I'm an eighteenth-generation sorceress, baby!
                        animegirl: Now, get ready for my love spell. One, two, three...
                        (Oh no!)
                        <<jump LOSE_got_relationship>>
    -> She sucks.
        <<SetCharacter GameManager animegirl 3>>
        animegirl: Hey, that's my BEST FRIEND.
        animegirl: I'm gonna give you 10 seconds to apologize. Do it!
        <<jump apologizeToAnimegirl>>
===

title: apologizeToAnimegirl
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
animegirl: So? Are you gonna apologize?
    -> No! I'm tired of you bossing me around!
        <<SetCharacter GameManager animegirl 1>>
        animegirl: ...
        animegirl: Not gonna lie, I respected that you stood up to me.
        animegirl: However, I'm not interested in guys that I can't boss around. So...
        animegirl: Have a nice life?
        (I'm free!)
        <<jump afterSchool>>
    -> Fine, I apologize! I'm sorry!
        <<SetCharacter GameManager animegirl 2>>
        animegirl: Aww, I love it when they grovel!
        animegirl: You just tick all of the boxes, don't you, playerName? The more I talk to you, the more I like you!
        (Uh oh...)
        animegirl: Ditch this class! Let's go hang out at the arcade.
        (Well, at least that sounds kind of fun.)
        <<jump LOSE_got_relationship>>
===

title: afterSchool
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
Well, time to actually sit down and learn. On the last day of the semester. Kind of dumb that we have to do that.
...
...
<<playSound AudioManager false school_bell>>
The bell just rang.
It's over! I'm free! I'm--
<<SetCharacter GameManager animeboy 1>>
animeboy: PlayerName!
(NO!!!)
animeboy: Please, playerName. I just want to talk. I feel horrible about how we left things yesterday.
    -> *step on his shoe*
        animeboy: ...
        animeboy: Wow, you're going full aggro right off the bat, huh?
        <<StartFPS GameManager>>
    -> I'll never trust you again. You betrayed the Grass Grazing Code.
        animeboy: I'm sorry! It's just...the more time I spend hanging around this school...
        <<SetCharacter GameManager animeboy 2>>
        animeboy: ...the more the idea of relationships appeals to me.
            -> Maybe you should try being in a relationship with someone else.
                animeboy: True...but you're SO much cooler, playerName!
                animeboy: Besides, every time I walk in this school, at least three people approach me and try to start a relationship.
                animeboy: It's just nice to be the one doing the pursuing for once.
                    -> Feel.
                        animeboy: Right?! Come on! Let's go to 7-Eleven and get some Slurpees.
                        (I'm tired. I'm just gonna roll with it.)
                        <<jump LOSE_got_relationship_animeboy>>
                    -> Can't relate. You're the same as everybody else.
                        animeboy: Wow. I never thought of it like that. Now I feel like an asshole.
                        animeboy: Okay. I respect your decision. I'm gonna go ask animegirl if she's free tonight.
                        <<jump DAY_SUCCESS_day3>>
            -> You are a conniving louse!
                animeboy: Hey now, there's no need for insults. I'm just being true to my feelings.
                animeboy: And saying my gamertag out loud in public is actually really embarrassing and not cool of you.
                animeboy: You picked the obvious FPS option, so...I guess we're gonna fight now.
                <<StartFPS GameManager>>
            -> I'm just...not really into shoes, my guy.
                animeboy: Oh. Yeah, that's kind of a dealbreaker.
                animeboy: Okay. Enjoy your summer. Let me know if you want to play Animal Crossing together.
                <<jump DAY_SUCCESS_day3>>
===

title: BeginDay
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
All I gotta do is stop myself from getting in a relationship! Then I'll have a lot of time to do whatever I want.
Okay playerName, just take it one day at a time. Avoid all the flirty students so you can get home safe...
...WITH NO RELATIONSHIP.
Well, here goes nothing.
<<SetBackground GameManager classroom>>
<<playSound AudioManager true classroom>>
...
<<SetCharacter GameManager animeboy 0>>
<<jump meet_animeboy>>
===

title: meet_animeboy
tags:
position: -301,-663
---
<<Checkpoint GameManager>>
Oh, it's animeboy, my childhood friend. He seems excited to see me.
animeboy: Hey, man! Check out my new sneaks!
animeboy shows me his new sneakers.
animeboy: So, are you excited for summer?
    -> Yeah, I can't wait!
        animeboy: Me neither. I have so many games I want to play.
    -> I'm nervous. I'm scared I'll get in a relationship.
        animeboy: Yeah, it's such a drag. All anybody thinks about at this school is dating.
    -> I'm single and ready to never mingle!
        animeboy: That's the spirit! It's gonna be a great summer!
animeboy: Well, I guess we'd better sit down. Class starts soon. Wait...someone's coming over here.
<<SetCharacter GameManager animegirl 2>>
animegirl: Hiii playerName!
(Crap!)
animegirl: Whaaat? Why are you looking at me like you're not happy to see me?
<<SetCharacter GameManager animeboy 0>>
animeboy: This is bad! Be careful what you say to her, playerName! Don't get in a relationship!
<<declare $first_fps = true>>
<<SetCharacter GameManager animegirl 0>>
animegirl: Well, playerName? Aren't you happy to see me?
    -> No, you're ugly and I hate you.
        <<SetCharacter GameManager animegirl 4>>
        animegirl: WHAT DID YOU SAY?
        animegirl: I'M GONNA REARRANGE YOUR FACE.
        <<StartFPS GameManager>>
    -> No, I am not attracted to you in any way whatsoever. Pig.
        <<SetCharacter GameManager animegirl 4>>
        animegirl: Well, maybe I need to fix your eyesight!
        animegirl: I'M GONNA PUMMEL YOU.
        <<StartFPS GameManager>>
    -> No, go back to the depths of hell where you belong.
        <<SetCharacter GameManager animegirl 4>>
        animegirl: LOOKS LIKE I NEED TO TEACH YOU SOME RESPECT, PLAYERNAME.
        <<StartFPS GameManager>>
<<SetCharacter GameManager animegirl 2>>
<<jump GoToClass>>
===

title: firstFPSOver
tags:
position: -823,-280
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animeboy 0>>
animeboy: Okay, okay, break it up.
animeboy: Come on, playerName. You can't be that rude when you're rejecting someone!
animeboy: If you do, they'll attack you. Be careful!
animeboy: Did you get hurt when you guys fought? Here, I have some bandaids. Patch yourself up.
<<RefreshHealth GameManager -1>>
animeboy: Okay, time for class to start. Stay focused and don't get in a relationship, okay? But don't get yourself killed, either!
<<jump GoToClass>>
===

title: GoToClass
tags:
position: -412,-435
---
<<Checkpoint GameManager>>
<<SetBackground GameManager classroom>>
<<playSound AudioManager true classroom>>
<<SetCharacter GameManager me>>
...
...
...
Whew, class is finally over. Now I can--
<<SetCharacter GameManager animegirl 2>>
animegirl: Oh, hello!
Ugh, it's animegirl again. She was in my homeroom last year. She has a bad temper and hates being single. She's probably gonna ask me out every time she sees me.
She's an internationally-renowned kickboxing champion with over 400 hours of elite weapons training. I have to be careful not to make her too mad...
animegirl: So, what are you up to? Teehee!
    -> Gross, go away
        <<jump GrossGoAway>>
    -> Hello m'lady *tips hat*
        <<SetCharacter GameManager animegirl 4>>
        animegirl: Ewww, stop acting like that! I'm serious, I'll wallop you!
        animegirl shoves you.
        <<set $been_shoved = true>>
        <<TakeDamage GameManager 10>>
        Ouch! My HP decreased...should I stop?
        -> Oh, pardon me, m'lady *bows*
            animegirl: That's it, I warned you!
            <<StartFPS GameManager>>
        -> Sheesh, learn how to take a joke.
            animegirl: Ugh, anyways...
            <<jump GrossGoAway>>
    -> ...
        <<jump dotdotdot>>
    -> Hey, did you know that the mitochondria is the powerhouse of the cell?
        <<jump mitochondria>>
===

title: GrossGoAway
tags:
position: -561,-209
---
<<Checkpoint GameManager>>
        <<SetCharacter GameManager animegirl 3>>
        animegirl: Wow, there's no need to be rude. I just wanted to ask if you were busy tonight...
            -> I'm already taken, I'm with otheranimegirl
                <<ChangeAffection GameManager animegirl -31>>
                <<SetCharacter GameManager animegirl 1>>
                <<LogAffectionChangingDialogue GameManager "negative">>
                animegirl: Ugh, her? Why? Whatever, I don't care.
                (That was close...)
                <<jump otheranimegirl>>
            -> I'm busy not hanging out with losers like you.
                <<ChangeAffection GameManager animegirl -10>>
                <<LogAffectionChangingDialogue GameManager "negative">>
                animegirl: Oh, I see how it is.
                <<SetCharacter GameManager animegirl 4>>
                animegirl: You're...trying to fight, then?
                (This might be bad.)
                -> I'm just trying to be left alone. I meant no harm.
                    <<SetCharacter GameManager animegirl 3>>
                    animegirl: Really? Because those insults sounded a little too natural coming out of your mouth.
                    <<TakeDamage GameManager 10>>
                    animegirl shoves you.
                    <<set $been_shoved = true>>
                    (Ow, that hurt! I lost some HP!)
                    <<jump PostShove>>
                -> Look, I drank two gallons of expired milk and I'm about to blast off.
                    <<ChangeAffection GameManager animegirl -10>>
                    <<LogAffectionChangingDialogue GameManager "negative">>
                    <<SetCharacter GameManager animegirl 1>>
                    <<set $has_gross_buff = true>>
                    animegirl: Ewww. Why were you drinking expired milk??? Oh god, I can smell it.
                    animegirl: You're kind of gross, you know that? What's wrong with you?
                    animegirl: I'm gonna tell everyone how gross you are. You'll never live this down.
                    <<AddGrossnessBuff GameManager>>
                    (My pride is wounded but my body is safe.)
                    (I am now considered Gross. People will hate me easily, but they'll also be too disgusted to hit me hard...)
                    <<jump awkward>>
            -> Uh...I gotta go outside. Don't follow me!
                <<SetBackground GameManager courtyard>>
                <<jump Outside>>
===

title: dotdotdot
tags:
position: -823,-280
---
<<Checkpoint GameManager>>
animegirl: Wow, the strong silent type. This actually makes me like you more!
    -> AHHHHHHHHH!
        <<jump WhatsWrong>>
    -> ...
        animegirl: Okay, now I REALLY like you! It's official, we're now dating. I'll tell everyone right away!
        -> *stammer*
            animegirl: Shhh! You don't need to say anything. We're going to have an amazing rest of our lives together!
            <<jump LOSE_got_relationship>>
        -> I would never date you, you foul creature!
            <<SetCharacter GameManager animegirl 4>>
            animegirl: WHAT DID YOU JUST SAY?
            animegirl takes out her earrings.
            animegirl: Get ready to get PUMMELED.
            <<StartFPS GameManager>>
        -> Yeah, I'll pass on that. Thanks, though!
            <<jump PostShove>>
    -> *fart*
        <<SetCharacter GameManager animegirl 4>>
        animegirl: What is wrong with you?! Why are you so disgusting!
        animegirl: Somebody's gotta teach you a lesson. Looks like it's gonna be ME!
        <<StartFPS GameManager>>
    -> Sorry, I was thinking about my extensive collection of banana peels
        <<SetCharacter GameManager animegirl 1>>
        animegirl: ...Okay. Gotta go! Nice seeing you!
        <<jump DAY_SUCCESS>>
===

title: Outside
position: -286,445
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 2>>
animegirl: I know you said not to follow you, but I got bored waiting so I did it anyways.
    -> Dude...
        animegirl: Yeah, my therapist says I don't handle boundaries well or something like that. I don't really pay attention.
        animegirl: Wait...we're in the courtyard. Does that mean you're...gonna confess to me?
        animegirl: This is so romantic!
            -> Oh no!
                <<SetCharacter GameManager animegirl 0>>
                animegirl: Too bad! We're meeting my parents tonight!
                <<jump LOSE_got_relationship>>
            -> Yeah, I'm confessing that you're annoying
                <<SetCharacter GameManager animegirl 4>>
                animegirl: Stop playing with my heart! You can't just insult someone like that.
                animegirl puts on her brass knuckles.
                animegirl: ...especially not ME!
                <<StartFPS GameManager true>>
            -> No, I just wanted to be alone...my goldfish died
                <<SetCharacter GameManager animegirl 1>>
                animegirl: Oh, I'm so sorry...I'll leave you alone.
                <<SetCharacter GameManager animegirl 2>>
                animegirl: For now...
                <<jump DAY_SUCCESS>>
===

title: otheranimegirl
position: -624,170
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
otheranimegirl: Wait, did you tell animegirl we were dating?
<<if $met_otheranimegirl == false>>
    That's otheranimegirl. She's known for having serious jealousy issues...she's really sweet when you talk to her, but I shouldn't cross her.
    I mean, she's an Olympic gold-medalist in archery.
    And there are also rumors her dad is in the Special Forces and trained her in seventeen forms of hand-to-hand combat.
    I'd better be careful...
    <<set $met_otheranimegirl = true>>
<<endif>>
otheranimegirl: So...what are we?
    -> I was just trying to get animegirl to back off. I can't stand her.
        <<SetCharacter GameManager otheranimegirl 1>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        otheranimegirl: Oh...she's actually my best friend...
        otheranimegirl: I mean...yeah, she's annoying and her feet smell. But I can't just let you trash her like that.
            -> Well, consider her trashed.
                otheranimegirl: Look, I'm glad you can see that she's ugly and weird, but all the other girls are gonna flame me for being a pickme if I just let you say that.
                otheranimegirl: So...I guess I have to fight you.
                <<StartFPS GameManager>>
            -> Okay, I'll stop.
                <<SetCharacter GameManager otheranimegirl 0>>
                otheranimegirl: Okay! Sorry, I just don't want to be clowned on for not defending my loser friend.
                <<SetCharacter GameManager otheranimegirl 2>>
                otheranimegirl: So...does that mean you're still single? Because I'm still single...
                <<jump otheranimegirlAvoid>>
            -> She's pretty and all, I just don't want a relationship.
                <<SetCharacter GameManager otheranimegirl 4>>
                otheranimegirl: Oh, so you think SHE'S pretty?
                otheranimegirl: I'm ten times the girl she is! Maybe you need to fix your eyesight!
                <<StartFPS GameManager>>
    -> Leave me alone, I just want to play Minecraft Pocket Edition
        <<jump OMGILoveThatGame>>
    -> I'm a compulsive liar, I can't control it, I need help, please help me (help me help me help me)
        <<SetCharacter GameManager otheranimegirl 3>>
        <<ChangeAffection GameManager otheranimegirl -10>>
        <<LogAffectionChangingDialogue GameManager "negative">>
        otheranimegirl: Not my circus, not my monkeys. I'm gonna leave now.
        <<jump DAY_SUCCESS>>
    -> AHHHHHHHHHH
        <<jump WhatsWrong>>
===

title: otheranimegirlAvoid
position: -1213,571
---
<<Checkpoint GameManager>>
otheranimegirl: Maybe we can hang out sometime!
    -> I'm too busy, I gotta study for an exam
        otheranimegirl: Awww, okay. Let's talk soon, though!
        <<jump DAY_SUCCESS>>
    -> Maybe you, me, and animegirl can all hang out together!
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: What does SHE have anything to do with US?
        otheranimegirl: I knew there was something going on between you two! I can't trust either of you!
        otheranimegirl: Get ready for a world of HURT!
        <<StartFPS GameManager>>
    -> Yeah, we can go to a pickle museum. I am incredibly interested in pickles.
        <<SetCharacter GameManager otheranimegirl 0>>
        otheranimegirl: Uh...sure!
        otheranimegirl: OR...we can go to the store next door to the pickle museum.
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: The jewelry store! Let's go pick out my engagement ring!
        <<jump LOSE_got_relationship_otheranimegirl>>
===

title: LOSE_got_relationship
position: -1321,-656
---
<<SetCharacter GameManager me>>
<<SetBackground GameManager kids>>
<<set $got_relationship = $got_relationship + 1>>
The next few years feel like a blur...
First, she threw out all of my gaming consoles and forced me to get into hiking instead. Every day is torture.
Next, she demanded that I major in Economics instead of Computer Science - Games.
This coincided with the release of ChatNFT, which destroyed the job market for all business and finance-related professionals. I now make a living selling balloon animals.
After the birth of our seventh child, we had to take out a third mortgage on our home.
Finally, she made a very crass comment to my mother during a family picnic, which completely destroyed my relationship with my parents.
I haven't seen them in years...
It makes me wonder...wonder what life would be like if I never got into a relationship.

<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<playSound AudioManager true home>>
<<SetCharacter GameManager me>>
...
*gasp* What happened?
What a nightmare...I got into a relationship?!
I hope that never happens again...
<<if $day == 1>>
    <<jump GoToClass>>
<<endif>>
<<if $day == 2>>
    <<jump Day2>>
<<endif>>
<<if $day == 3>>
    <<jump day3_otheranimegirl>>
<<endif>>

===

title: LOSE_got_relationship_otheranimegirl
position: -1318,-518
---
<<SetCharacter GameManager me>>
<<SetBackground GameManager cheese>>
<<set $got_relationship = $got_relationship + 1>>
The next few years feel like a blur...
First, she decided to embrace a technology-free lifestyle. I now live by candlelight and have nearly died of E. coli four times from eating unrefrigerated meats.
Next, she committed wire fraud in my name and demanded I take the fall for it. I spent twelve years in prison.
Those twelve years were probably the highlight of my life...
After my release from prison, she insisted that we refinance our home to start a business making artisinal cheeses.
Three weeks after the failure of Oasis Meadow Moonglow CheesesÂ© due to multiple food poisoning-related deaths, she left the back door open and my dog ran away.
I haven't seen my sweet doggy boy since...
It makes me wonder...wonder what life would be like if I never got into a relationship.

<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<playSound AudioManager true home>>
...
*gasp* What happened?
What a nightmare...I got into a relationship?!
I hope that never happens again...
<<if $day == 1>>
    <<jump GoToClass>>
<<endif>>
<<if $day == 2>>
    <<jump Day2>>
<<endif>>
<<if $day == 3>>
    <<jump day3_otheranimegirl>>
<<endif>>
===

title: LOSE_got_relationship_animeboy
position: -1318,-518
---
<<SetCharacter GameManager me>>
<<SetBackground GameManager shrimp>>
<<set $got_relationship = $got_relationship + 1>>
The next few years feel like a blur...
At first, everything was okay. We were friends for so long that being in a relationship wasn't that crazy. I even started to share his appreciation for shoes.
Then, he became really, really emotionally attached to his pet shrimp.
He set up one aquarium for shrimp. Then another. Then another.
Within a year, we had 308 shrimp tanks in our apartment.
Our neighbor called Animal Control, so I applied for him to be on a TV show for animal hoarding. He got rid of 300 of his shrimp tanks, but accumulated 350 more within six months.
I was finding shrimp in my shoes. In my hair. In my mouth when I woke up in the morning.
When I finally decided to leave him, I had to live in a box because I never got a job after high school. He was supporting me all that time as a software engineer.
That last part was my bad, actually. I shouldn't have been such a deadbeat.
Still, it makes me wonder...wonder what life would be like if I never got into a relationship.

<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<playSound AudioManager true home>>
...
*gasp* What happened?
What a nightmare...I got into a relationship?!
I hope that never happens again...
<<if $day == 1>>
    <<jump GoToClass>>
<<endif>>
<<if $day == 2>>
    <<jump Day2>>
<<endif>>
<<if $day == 3>>
    <<jump day3_otheranimegirl>>
<<endif>>
===

title: transitionBack
position: -879,-843
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<RefreshHealth GameManager -1>>
<<set $lost_fps = $lost_fps + 1>>
<<playSound AudioManager true home>>
...
*gasp* What happened?
What a nightmare...I remembered fighting something?!
That's probably just a bad dream...anyways, I should get ready for school!
<<if $day == 1>>
    <<jump GoToClass>>
<<endif>>
<<if $day == 2>>
    <<jump Day2>>
<<endif>>
<<if $day == 3>>
    <<jump day3_otheranimegirl>>
<<endif>>
===

title: EnterFPS
position: -879,-1134
---
<<Checkpoint GameManager>>
...
What's wrong with this girl...?
Next Line
<<ResumeFPS GameManager>>
===

title: ExitFPS
position: -878,-989
---
<<Checkpoint GameManager>>
yourenemygirl: ONE LAST CHANCE! ADMIT YOU ARE TRASH!!!
    -> I admit defeat, I am, in fact, trash.
        <<LogLastChance GameManager false>>
        <<EndFPS GameManager false>>
    -> As long as you're happy, I can be anything. Please, give me one more chance.
        // -1 here represents setting to StartingHealth
        <<LogLastChance GameManager true>>
        <<RefreshHealth GameManager -1>>
        <<ResumeFPS GameManager>>
===

title: FPS_SUCCESS_animegirl
position: -876,-1483
---
<<Checkpoint GameManager>>
<<set $fps_won = $fps_won + 1>>
animegirl: ...
animegirl: Wait, I think I spaced out...what happened??
    -> No idea.
        animegirl: Oh, teehee!
        animegirl: Anyways...
    -> You went insane and tried to kill me!!!
        animegirl: I don't remember that at all...
        animegirl: Anyways...
<<ContinueFromPreFPSNode GameManager>>
===

title: FPS_SUCCESS_otheranimegirl
position: -878,-1338
---
<<Checkpoint GameManager>>
<<set $fps_won = $fps_won + 1>>
otheranimegirl: ...
otheranimegirl: Heehee, what happened? I'm all dizzy...
    -> Bruh.
        otheranimegirl: Teeheehee!
        otheranimegirl: Anyways...
    -> You went insane and tried to kill me!!
        otheranimegirl: Don't be silly! I'd never do something like that!
        otheranimegirl: Anyways...
<<ContinueFromPreFPSNode GameManager>>
===

title: FPS_SUCCESS_animeboy
position: -878,-1338
---
<<Checkpoint GameManager>>
<<set $fps_won = $fps_won + 1>>
animeboy: ...
animeboy: Huh, what's going on? My head hurts...
    -> Bruh.
        animeboy: I hope I didn't like. Attack you or anything.
        animeboy: Anyways...
    -> You went insane and tried to kill me!!
        animeboy: No way, I don't believe I'd do that!
        animeboy: Anyways...
<<ContinueFromPreFPSNode GameManager>>
===

title: DAY_SUCCESS
position: 1434,170
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
<<playSound AudioManager false school_bell>>
The bell just rang.
...
The day is finally over!

<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<playSound AudioManager true home>>
Ah, nice to be back home.
Good thing I didn't get into a relationship today.
...
Oh man, would you look at the time?
I should get ready for bed...
<<SetBackground  GameManager black>>
<<playSound AudioManager true mysterious_voice>>
zzz
zzz
zzz
<<LogDayEndAnalytics GameManager>>
<<ProgressDay GameManager>>
===
title: PostShove
position: -1146,17
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 3>>
animegirl: I'm not going to let some silly little boy talk to me like that.
    -> You're right, I'm a silly little boy. I'm so silly.
        animegirl: That's what I thought.
        <<SetCharacter GameManager animegirl 1>>
        animegirl: I won't fight you, but I will say this...I don't respect you at all. I'm going to tell everyone I know what a spineless little worm you are.
        (I hope this doesn't have any repercussions for me...)
        <<jump DAY_SUCCESS>>
    -> Well, I'm not gonna let a dumb little girl tell me what to do!
        <<SetCharacter GameManager animegirl 4>>
        <<ChangeAffection GameManager animegirl -10>>
        <<LogAffectionChangingDialogue GameManager "negative">>
        animegirl: I am going to MESS YOU UP.
        <<StartFPS GameManager>>
    -> I'm so sorry, otheranimegirl. I was rude. Will you please forgive me?
        <<SetCharacter GameManager animegirl 0>>
        <<ChangeAffection GameManager animegirl 20>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        animegirl: Mmm...well, that's okay, I guess.
        animegirl: I think an argument every now and then is key to a healthy relationship.
        (Uh oh...)
        <<SetCharacter GameManager animegirl 2>>
        animegirl: I'll text you tonight, okay? You can tell me all about your bad day!
        <<jump LOSE_got_relationship>>
===
title: mitochondria
position: -304,-243
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 2>>
animegirl: Ooh, you're so smart and funny!
    -> Uh...I gotta go outside. Don't follow me.
        <<SetBackground GameManager courtyard>>
        <<jump Outside>>
    -> I'm not smart. I'm just really...REALLY into mitochondria
        <<jump PowerYourCells>>
===
title: PowerYourCells
position: 198,240
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 0>>
animegirl: ...Okay! So, what are you doing tonight?
    -> Blood magic.
        animegirl: Oh, really?
        animegirl: So where do you get your goat heads? Or do you prefer human sacrifice for your rituals?
            -> Uh...what???
                <<SetCharacter GameManager animegirl 3>>
                animegirl: Ugh. I knew it. You're not a REAL practicioner of the ancient arts of darkness.
                animegirl: I bet you can't even reanimate corpses.
                Okay...I REALLY need to avoid this girl. I'm scared.
                    -> AHHHHHHHHH
                        <<jump WhatsWrong>>
                    -> Return to the abyss from whence you came, demon!
                        <<SetCharacter GameManager animegirl 1>>
                        animegirl: That's actually kind of rude and culturally insensitive, playerName.
                        animegirl: Feeling alienated by my peers for my beliefs is really hurtful. It's messed up.
                        animegirl: Anyways, I'm gonna kick the crap out of you now. Get ready.
                        <<StartFPS>>
    -> Homework! I'm a huge nerd!
        <<SetCharacter GameManager animegirl 2>>
        animegirl: Seeeeee! I knew you were super smart! Why did you lie to me before?
        animegirl: You should help me with MY homework!
        animegirl: I'll come over at 7 for our study date!
        <<jump LOSE_got_relationship>>
    -> I'm not interested in you. You big dumb.
        <<TakeDamage GameManager 10>>
        <<SetCharacter GameManager animegirl 3>>
        animegirl: Oh, so you think you're tough, huh?
        animegirl shoves you.
        <<set $been_shoved = true>>
        <<TakeDamage GameManager 10>>
        (Ow, that hurt! I lost some HP!)
        <<jump PostShove>>
===
title: WhatsWrong
position: -768,561
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Is something wrong?
<<if $met_otheranimegirl == false>>
    That's otheranimegirl. She's known for having serious jealousy issues...she's really sweet when you talk to her, but I shouldn't cross her.
    I mean, she's an Olympic gold-medalist in archery.
    And there are also rumors her dad is in the Special Forces and trained her in seventeen forms of hand-to-hand combat.
    I'd better be careful...
    <<set $met_otheranimegirl = true>>
<<endif>>
otheranimegirl: You just screamed out loud all of a sudden...
    -> I was just trying to get animegirl to back off. I can't stand her.
        <<SetCharacter GameManager otheranimegirl 1>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        otheranimegirl: Oh...she's actually my best friend...
        otheranimegirl: I mean...yeah, she's annoying and her feet smell. But I can't just let you trash her like that.
            -> Well, consider her trashed.
                otheranimegirl: Look, I'm glad you can see that she's ugly and weird, but all the other girls are gonna flame me for being a pickme if I just let you say that.
                otheranimegirl: So...I guess I have to fight you.
                <<StartFPS GameManager>>
            -> Okay, I'll stop.
                <<SetCharacter GameManager otheranimegirl 0>>
                otheranimegirl: Okay! Sorry, I just don't want to be clowned on for not defending my loser friend.
                <<SetCharacter GameManager otheranimegirl 2>>
                otheranimegirl: So...does that mean you're still single? Because I'm still single...
                <<jump otheranimegirlAvoid>>
            -> She's pretty and all, I just don't want a relationship.
                <<SetCharacter GameManager otheranimegirl 4>>
                otheranimegirl: Oh, so you think SHE'S pretty?
                otheranimegirl: I'm ten times the girl she is! Maybe you need to fix your eyesight!
                <<StartFPS GameManager>>
    -> I'm just having a really bad day.
        <<SetCharacter GameManager otheranimegirl 0>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        otheranimegirl: Oh, I'm sorry to hear that. I hope things get better for you.
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: If you ever wanna talk...I'm here.
        <<jump DAY_SUCCESS>>
    -> I just want to play Halo Reach Legendary Edition Limited Edition Box Set. Please leave me alone.
        <<jump OMGMyFavoriteGame>>
    -> No. but honestly a cheeseburger sounds really good right now.
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: Mmm, I think so too! Let's go together!
        <<jump LOSE_got_relationship_otheranimegirl>>
===
title: OMGILoveThatGame
position: -483,897
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 0>>
otheranimegirl: Oh, I love that game!
    -> ...That game sucks, I hate it.
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: Those are fighting words. Get ready.
        <<StartFPS GameManager>>
    -> I know, right? It's the best game ever!
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: Let's play it together tonight, just you and me, boyfriend!
        <<jump LOSE_got_relationship_otheranimegirl>>
    -> ... I was just kidding. I don't even know what that game is.
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: Oh, okay. I'll see you later, then.
        <<jump DAY_SUCCESS>>
===

title: OMGMyFavoriteGame
position: -483,897
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 0>>
otheranimegirl: That's my FAVORITE game, playerName!
    -> ...That game sucks, I hate it.
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: Those are fighting words. Get ready.
        <<StartFPS GameManager>>
    -> I know, right? It's the best game ever!
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: Let's play it together tonight, just you and me, boyfriend!
        <<jump LOSE_got_relationship_otheranimegirl>>
    -> ... I was just kidding. I don't even know what that game is.
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: Oh, okay. I'll see you later, then.
        <<jump DAY_SUCCESS>>
===

title: awkward
position: -987,261
---
<<Checkpoint GameManager>>
animegirl: ...So what now?
-> AHHHHHHHHHHHHHHHHHHH
    <<jump WhatsWrong>>
-> I'm gonna step outside...Don't follow me.
    <<SetBackground GameManager courtyard>>
    <<jump Outside>>
<<StartFPS GameManager>>
===

title: Day2
tags:
position: 240,-1001
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
<<SetBackground GameManager classroom>>
<<playSound AudioManager true classroom>>
<<set $day = 2>>
otheranimegirl: Oh, hey! It's you!
<<if $met_otheranimegirl == false>>
    That's otheranimegirl. She's known for having serious jealousy issues...she's really sweet when you talk to her, but I shouldn't cross her.
    I mean, she's an Olympic gold-medalist in archery.
    And there are also rumors her dad is in the Special Forces and trained her in seventeen forms of hand-to-hand combat.
    I'd better be careful...
    <<set $met_otheranimegirl = true>>
<<endif>>
otheranimegirl: I cracked my iPhone screen, so I can't bedrot and play mobile games all day. I've just been hanging out at school gossiping.
otheranimegirl: I've heard a lot about you...
    -> Let me guess, you heard about the expired milk thing. <<if $has_gross_buff == true>>
        <<jump EwThatWasTrue>>
    -> I hope you didn't hear anything bad. I get bullied a lot...<<if $been_shoved == true>>
        <<jump Tattle>>
    -> Yeah, you've probably heard that I've been rejecting every dumb girl at this school.
        otheranimegirl: Oooh, I love a challenge.
        <<jump otheranimegirlFlirt>>
    -> pineapple hurt mouth
        <<jump Pineapple>>
===

title: Pineapple
tags:
position: -10,-820
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Pineapple...?
    -> pineapple hurt mouth
        otheranimegirl: I don't understand what's happening.
            -> pineapple hurt mouth
                otheranimegirl: ...I'm gonna go.
                <<jump animeboy_day2>>
            -> Anyways...what's up?
                otheranimegirl: Oh...uh...
                <<jump otheranimegirlFlirt>>
===

title: Tattle
tags:
position: 601,-582
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Oh no! Poor baby...who bullies you?
    -> Animegirl does! She shoved me, and I got a booboo.
        otheranimegirl: Really?! She's my best friend...I can't believe she'd do this.
        otheranimegirl: Actually, what am I saying? Yes, I can. She sucks.
        <<SetCharacter GameManager otheranimegirl 0>>
        otheranimegirl: Here, I can give you a little "boost" so your booboos don't hurt so much anymore.
        <<AddHealthBuff GameManager>>
        <<TakeDamage GameManager -20>>
        (She just injected me with something and I can feel my maximum health increasing. I feel totally amped!)
        (And also kind of itchy and paranoid. It's probably fine.)
        otheranimegirl: I got that off the internet with Bitcoin. It was $400, but hey, anything for you~
        <<jump otheranimegirlFlirt>>
    -> All of the girls at this dumb school keep bullying me for a date! I'm sick of it!
        <<jump DontWantGirlfriend>>
===

title: DontWantGirlfriend
tags:
position: 413,-772
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
<<ChangeAffection GameManager otheranimegirl -10>>
<<LogAffectionChangingDialogue GameManager "negative">>
otheranimegirl: I don't understand...are you saying you don't like me? Don't you think I'm pretty?
    -> Honestly, you look like a dog's butt.
        <<ChangeAffection GameManager otheranimegirl -30>>
        <<LogAffectionChangingDialogue GameManager "negative">>
        <<jump Danger>>
    -> I do think you're pretty, I just don't want a relationship.
        <<SetCharacter GameManager otheranimegirl 2>>
        <<ChangeAffection GameManager otheranimegirl 30>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        otheranimegirl: Wow, are you trying to politely and maturely communicate your feelings with me?
        otheranimegirl: That's so hot! I'm going to disregard everything you said after "I do think you're pretty".
        <<jump otheranimegirlFlirt>>

===

title: EwThatWasTrue
tags:
position: 383,-336
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
<<ChangeAffection GameManager otheranimegirl -20>>
<<LogAffectionChangingDialogue GameManager "negative">>
otheranimegirl: Woah, what? You actually did that? I thought animegirl was just making that up because you rejected her or something.
otheranimegirl: This whole time...I thought you were cute and cool. Now I'm totally grossed out.
<<TakeDamage GameManager 5>>
(I feel so bad about myself. The psychological damage is actually wounding me.)
<<jump Danger>>
===

title: Danger
tags:
position: 313,-181
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 4>>
otheranimegirl: You are SUCH a loser. I can't believe I wanted to ask you out!
    -> If you did ask me out, I'd just reject you for being a smelly ugly weirdo.
        otheranimegirl: Are you ready to be disemboweled? Because that's what's going to happen.
        <<StartFPS GameManager>>
    -> You have horrible taste in men, evidently.
        <<SetCharacter GameManager otheranimegirl 2>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        otheranimegirl: Huh. It sounds like you have extremely low self-esteem. I find that...quite attractive, actually.
        <<jump otheranimegirlFlirt>>

===

title: otheranimegirlFlirt
tags:
position: 31,-339
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
otheranimegirl: So...what are you doing later? I was hoping we could go see a movie...
    -> The only movie I want to watch is Beverly Hills Chihuahua.
        <<ChangeAffection GameManager otheranimegirl 40>>
        <<LogAffectionChangingDialogue GameManager "positive">>
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: That's so insanely weird...
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: ...because that is my absolute FAVORITE movie ever!!! I knew we were meant to be together!!!
        otheranimegirl: I'm updating all my socials so everyone knows we're dating. I can't WAIT to get married to you!
        (I'm done for...)
        <<jump LOSE_got_relationship_otheranimegirl>>
    -> I only watch Belgian arthouse films.
        otheranimegirl: Yeah, I'm not into that. You have fun, though.
        <<jump animeboy_day2>>
    -> I'm actually going to see a movie with animegirl later.
        <<ChangeAffection GameManager otheranimegirl -20>>
        <<LogAffectionChangingDialogue GameManager "negative">>
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: You're going out with my BEST FRIEND? You're scum. I can't believe you would do this to me.
        otheranimegirl: I expected this from her because she's a terrible person, but you? You betrayed me. I will end you.
        <<StartFPS GameManager>>

===

title: animeboy_day2
tags:
position: 601,-582
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
...
...
...
Ugh, finally, lunch time.
<<SetBackground GameManager cafeteria>>
<<SetCharacter GameManager animeboy 0>>
animeboy: Hey! I haven't seen you all day! What's up?
    -> I was accosted by otheranimegirl!
        animeboy: Oof, that's rough. She's really pushy, isn't she?
        animeboy: Well, maybe I can cheer you up...
        <<jump betrayal>>
    -> I keep getting my ass beat. I'm depressed. <<if $lost_fps > 1>>
        <<jump strengthBuff>>
    -> I'm just glad to see someone who isn't trying to date me.
        animeboy: Oh...about that...
        <<jump betrayal>>
===

title: strengthBuff
tags:
position: 601,-582
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animeboy 1>>
animeboy: Those girls are really roughing you up, huh? Sorry to hear that.
animeboy: Well...I don't know if this will help, but I brought my dad's special kombucha today. He's a powerlifter.
<<SetCharacter GameManager animeboy 0>>
animeboy: Here, you can have it. I've never tasted it because it smells like garbage water, but it shouldn't be too bad.
*chug chug chug*
That was completely disgusting.
<<AddDamageBuff GameManager>>
<<set $has_damage_buff = true>>
But...I feel stronger! My attacks should now do more Damage!
animeboy: Well, now that that's out of the way, I was wondering...
<<jump betrayal>>
===

title: betrayal
tags:
position: 601,-582
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animeboy 0>>
animeboy: I've been meaning to say this for a while, but I didn't have the courage.
animeboy: The truth is, I got expelled like four months ago for not wearing my school uniform. I've been sneaking onto campus just to talk to you.
animeboy: I just really, really like you, playerName. So...
<<SetCharacter GameManager animeboy 2>>
animeboy: Would you...be interested in going to dinner sometime?
    -> How could you?! I trusted you!
        <<SetCharacter GameManager animeboy 1>>
        animeboy: I'm sorry! We've been friends for ages, and I just really like you!
        animeboy: Please, just let me take you out to dinner. You'll see that we're perfect for each other!
            -> Actually, animeboy, you're right. I am in love with you.
                <<SetCharacter GameManager animeboy 2>>
                animeboy: I promise I won't let you down, playerName!
                <<jump LOSE_got_relationship_animeboy>>
            -> animeboy, I like girls.
                <<SetCharacter GameManager animeboy 1>>
                animeboy: Ok bro, let's not be delusional now.
                animeboy: We literally met because we both were fighting to avoid relationships with girls.
                    -> Not gonna lie, you have a point there.
                        <<jump convince>>
    -> You are the absolute lowest of the low. You disgust me.
        <<SetCharacter GameManager animeboy 3>>
        animeboy: I can't control my feelings, playerName. Maybe YOU'RE the weird one for being so insistent on avoiding relationships!
        animeboy: Fine, you want to insult me? Let's take this outside!
        <<StartFPS GameManager true>>
    -> I'm sorry bro, but I can't introduce you to my parents. You're a high school delinquent.
        <<SetCharacter GameManager animeboy 3>>
        animeboy: What is that supposed to mean? You think I'm not good enough for your family?
        animeboy: You're just like my dad! You think I'm not good enough, no matter how hard I try!
            -> Maybe your dad is right.
                <<SetCharacter GameManager animeboy 1>>
                animeboy: Bro, chill. That's a little too far, even for this game.
                animeboy: Anyways, you probably knew that was the FPS dialogue option. Let's just get it over with.
                <<SetCharacter GameManager animeboy 3>>
                animeboy: I'm gonna beat the crap out of you now.
                animeboy: First we gotta go outside, though. I don't want to get arrested.
                <<StartFPS GameManager true>>
            -> Sorry. I'm just not interested in...you.
                <<jump convince>>
            -> My stomach hurts...*gurgle gurgle* <<if $has_gross_buff == true>>
                <<SetCharacter GameManager animeboy 1>>
                animeboy: Bro...is it the expired milk thing?
                animeboy: Please do not get diarrhea on my sneakers, bro. These are brand new.
                animeboy: Look at how crisp these shoes are. Don't do it. Please.
                    -> *turn around and aim for his sneakers*
                        <<SetCharacter GameManager animeboy 3>>
                        animeboy: STOP! Okay, I'll leave you alone! Damn!
                        <<SetCharacter GameManager animeboy 1>>
                        animeboy: I'll see you tomorrow, okay? Bye!
                        <<jump DAY_SUCCESS_day2>>
                    -> *small poot*
                        animeboy: Was that it? Okay, moving on...
                        <<jump convince>>
                    -> Hnnnghh...I'll hold it in...
                        animeboy: You'd really put yourself through all that pain...just to protect my sneaks?
                        animeboy: ...I'm in love with you. Run away with me. I won't take no for an answer.
                        <<jump LOSE_got_relationship_animeboy>>
                
===

title: convince
tags:
position: 601,-582
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animeboy 0>>
animeboy: Come on, playerName. I promise you won't regret this.
animeboy: I mean, what else do you have to do right now?
    -> I have to go home and feed the rooster that wakes me up every morning. It's dying.
        <<SetCharacter GameManager animeboy 1>>
        animeboy: Sounds serious. Okay, see you tomorrow.
        <<jump DAY_SUCCESS_day2>>
    -> I have to find a dinner date that doesn't wear cheap knockoff sneakers.
        <<SetCharacter GameManager animeboy 3>>
        animeboy: You can call me ugly...you can call me stupid...you can call me any name you want...
        <<SetCharacter GameManager animeboy 4>>
        animeboy: But you can NEVER INSULT MY SNEAKS.
        animeboy: I'M TAKING YOU OUTSIDE.
        <<StartFPS GameManager true>>
    -> I have to go home and play video games.
        animeboy: ...Which video game?
            -> League of Legends.
                animeboy: Weak. Nevermind, I'll see you tomorrow.
                <<jump DAY_SUCCESS_day2>>
            -> Animal Crossing.
                <<SetCharacter GameManager animeboy 2>>
                animeboy: Dope. I'll walk home with you.
                <<jump LOSE_got_relationship_animeboy>>
===

title: DAY_SUCCESS_day2
position: 1434,170
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
<<playSound AudioManager false school_bell>>
The bell just rang.
...
The day is finally over! Just one more day until summer...

<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<playSound AudioManager true home>>
Ah, nice to be back home.
Good thing I didn't get into a relationship today.
...
Oh man, would you look at the time?
I should get ready for bed...
<<LogDayEndAnalytics GameManager>>
<<ProgressDay GameManager>>
===

title: DAY_SUCCESS_day3
position: 1657,167
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
...
Finally...
The school day is over! I gotta get home!
<<SetBackground GameManager home>>
<<playSound AudioManager true home>>
<<SetCharacter GameManager me>>
I'm...still single! I did it!
I can follow my dreams now...
<<if $fps_won > 2>>
    <<jump fight_ending>>
<<endif>>
<<if $got_relationship > 2>>
    <<jump rich_ending>>
<<endif>>
I can invest in crypto!
I can buy NFTs!
And best of all...I can stop spending 40 hours a week training at the gym just so I can have a chance at defending myself against these girls!
For the first time in ages, the future feels bright...
<<SetBackground GameManager beach>>
Years pass. I ended up amassing a fortune in cryptocurrency.
Using my newfound wealth, I bought a chateau in Barcelona and spend my days sipping wine and strolling on the beach.
Although I love my life, I wonder what would've happened if I did things a little differently...
What if I was a better fighter? What if I flirted some more?
What other paths could my life had taken?
Either way, I'm happy.
<<ProgressDay GameManager>>
===

title: fight_ending
position: -39,877
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
<<SetBackground GameManager dojo>>
I fought my way throughout high school...and I want to help others do the same.
I opened up my own dojo for those looking to defend their singlehood.
It's located on a tall mountain in rural Bulgaria, but still, hundreds of people make the pilgrimage here each year...
Of course, some of those people don't have the best intentions. An average of six people still ask for my number each week.
But still...the ones that come here to learn how to fight to stay single...they make my life worthwhile.
I would never have been able to accomplish this if I got in a relationship!
<<ProgressDay GameManager>>
===

title: rich_ending
position: 160,515
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
<<SetBackground GameManager running>>
Years later...I became the CEO of a successful company.
I also became extremely attractive after high school. Like, ridiculously so.
The amount of romantic advances I have to face each day are overwhelming, but it only bolsters my resolve to stay single.
Looking back, I always had this problem. I always said or did the wrong thing, and people fell in love with me...
If I could go back, maybe I could be more insulting. Or chosen my words more carefully...maybe then, I'd have a different life.
But honestly, I don't know if I'd want a different life. Being super rich and handsome is actually pretty cool. 
<<ProgressDay GameManager>>
===