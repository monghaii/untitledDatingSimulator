title: Start
tags:
position: 212,-726
---
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<Checkpoint GameManager>>

/// variable declarations
<<declare $been_shoved = false>>
<<declare $has_gross_buff = false>>
<<declare $has_health_buff = false>>
<<declare $day = 1>>
summer's over already?
i can't wait to keep playing Doki Doki Literature Club Plus! Premium Physical Edition again..
<<jump BeginDay>>
===

title: AnotherDay
tags:
position: 212,-726
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
ah, another day, another dol- I mean chance to ruin my life and get in a relationship...
I can't wait to keep playing Wii Sports Resort again..
<<if $day == 1>>
    <<set $day = 2>>
    I must stay vigilant and dodge these amorous vixens. Only then can I truly be happy and fulfilled.
    <<jump DayTwoStart>>
<<endif>>
<<if $day == 2>>
    <<set $day = 1>>
    <<jump BeginDay>>
<<endif>>
===

title: DayTwoStart
tags:
position: 211,-726
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
<<SetBackground GameManager classroom>>
otheranimegirl: Oh, hey! It's you! I've heard a lot about you...
    -> Let me guess, you heard about the expired milk thing. <<if $has_gross_buff == true>>
        <<jump EwThatWasTrue>>
    -> I hope you didn't hear anything bad. I get bullied a lot...<<if $been_shoved == true>>
        <<jump Tattle>>
    -> Yeah, you've probably heard that I've been rejecting every dumb girl at this school.
        otheranimegirl: Oooh, I love a challenge.
        <<jump otheranimegirlFlirt>>
    -> pineapple hurt mouth
        <<jump Pineapple>>
===

title: Pineapple
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Pineapple...?
    -> pineapple hurt mouth
        otheranimegirl: I don't understand what's happening.
            -> pineapple hurt mouth
                otheranimegirl: ...I'm gonna go.
                <<jump DAY_SUCCESS>>
===

title: Tattle
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
otheranimegirl: Oh no! Poor baby...who bullies you?
    -> Animegirl does! She shoved me, and I got a booboo.
        otheranimegirl: Really?! She's my best friend...I can't believe she'd do this.
        <<SetCharacter GameManager otheranimegirl 0>>
        otheranimegirl: Here, I can give you a little "boost" so your booboos don't hurt so much anymore.
        <<AddHealthBuff GameManager>>
        <<TakeDamage GameManager -20>>
        (She just injected me with something and I can feel my maximum health increasing. I feel totally amped!)
        (And also kind of itchy and paranoid. It's probably fine.)
        otheranimegirl: I got that off the internet with Bitcoin. It was $400, but hey, anything for you~
        <<jump otheranimegirlFlirt>>
    -> All of the girls at this dumb school keep bullying me for a date! I'm sick of it!
        <<jump DontWantGirlfriend>>
===

title: DontWantGirlfriend
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
<<ChangeAffection GameManager otheranimegirl -10>>
otheranimegirl: I don't understand...are you saying you don't like me? Don't you think I'm pretty?
    -> Honestly, you look like a dog's butt.
        <<ChangeAffection GameManager otheranimegirl -30>>
        <<jump Danger>>
    -> I do think you're pretty, I just don't want a relationship.
        <<SetCharacter GameManager otheranimegirl 2>>
        <<ChangeAffection GameManager otheranimegirl 30>>
        otheranimegirl: Wow, are you trying to politely and maturely communicate your feelings with me?
        otheranimegirl: That's so hot! I'm going to disregard everything you said after "I do think you're pretty".
        <<jump otheranimegirlFlirt>>

===

title: EwThatWasTrue
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 1>>
<<ChangeAffection GameManager otheranimegirl -20>>
otheranimegirl: Woah, what? You actually did that? I thought animegirl was just making that up because you rejected her or something.
otheranimegirl: This whole time...I thought you were cute and cool. Now I'm totally grossed out.
<<TakeDamage GameManager 5>>
(I feel so bad about myself. The psychological damage is actually wounding me.)
<<jump Danger>>
===

title: Danger
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 4>>
otheranimegirl: You are SUCH a loser. I can't believe I wanted to ask you out!
    -> If you did ask me out, I'd just reject you for being a smelly ugly weirdo.
        otheranimegirl: Are you ready to be disemboweled? Because that's what's going to happen.
        <<StartFPS GameManager>>
    -> You have horrible taste in men, evidently.
        <<SetCharacter GameManager otheranimegirl 2>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        otheranimegirl: Huh. It sounds like you have extremely low self-esteem. I find that...quite attractive, actually.
        <<jump otheranimegirlFlirt>>

===

title: otheranimegirlFlirt
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
otheranimegirl: So...what are you doing later? I was hoping we could go see a movie...
    -> The only movie I want to watch is Beverly Hills Chihuahua.
        <<ChangeAffection GameManager otheranimegirl 40>>
        <<SetCharacter GameManager otheranimegirl 1>>
        otheranimegirl: That's so insanely weird...
        <<SetCharacter GameManager otheranimegirl 2>>
        otheranimegirl: ...because that is my absolute FAVORITE movie ever!!! I knew we were meant to be together!!!
        otheranimegirl: I'm updating all my socials so everyone knows we're dating. I can't WAIT to get married to you!
        (I'm done for...)
        <<jump LOSE_got_relationship>>
    -> I only watch Belgian arthouse films.
        otheranimegirl: Yeah, I'm not into that. You have fun, though.
        <<jump DAY_SUCCESS>>
    -> I'm actually going to see a movie with animegirl later.
        <<ChangeAffection GameManager otheranimegirl -20>>
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: You're going out with my BEST FRIEND? You're scum. I can't believe you would do this to me.
        otheranimegirl: I expected this from her because she's a terrible person, but you? You betrayed me. I will end you.
        <<StartFPS GameManager>>

===

title: BeginDay
tags:
position: -32,-518
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
ok playerName, just take it one day at a time. Just avoid all the girls so you can get home safe.
WITH NO RELATIONSHIP.
well... here goes nothing...
<<SetBackground GameManager classroom>>
...
<<SetCharacter GameManager animegirl 2>>
<<jump GoToClass>>
===

title: GoToClass
tags:
position: -209,-361
---
<<Checkpoint GameManager>>
animegirl: Oh, hello!
    -> Gross, go away
        <<jump GrossGoAway>>
    -> Hello m'lady
        <<SetCharacter GameManager animegirl 4>>
        animegirl: Oh ew no.
        <<StartFPS GameManager>>
    -> ...
        <<jump dotdotdot>>
    -> Hey, did you know that the mitochondria is the powerhouse of the cell?
        <<jump mitochondria>>
===

title: GrossGoAway
tags:
position: -327,-115
---
<<Checkpoint GameManager>>
        <<SetCharacter GameManager animegirl 3>>
        animegirl: Wow, there's no need to be rude. I just wanted to ask if you were busy tonight...
            -> I'm already taken, I'm with otheranimegirl
                <<ChangeAffection GameManager animegirl -31>>
                <<SetCharacter GameManager animegirl 1>>
                animegirl: My bad. I thought you were single.
                (Wow that was close)
                <<jump otheranimegirl>>
            -> I'm busy not hanging out with losers like you.
                <<ChangeAffection GameManager animegirl -10>>
                animegirl: Oh, I see how it is.
                <<SetCharacter GameManager animegirl 4>>
                animegirl: You're...trying to fight, then?
                (This might be bad.)
                -> I'm just trying to be left alone. I meant no harm.
                    <<SetCharacter GameManager animegirl 3>>
                    animegirl: Really? Because those insults sounded a little too natural coming out of your mouth.
                    <<TakeDamage GameManager 10>>
                    animegirl shoves you.
                    <<set $been_shoved = true>>
                    (ow, that hurt! my HP is now lower! yarrr yarrr ayeee)
                    <<jump PostShove>>
                -> Look, I drank two gallons of expired milk and I'm about to blast off.
                    <<ChangeAffection GameManager animegirl -10>>
                    <<SetCharacter GameManager animegirl 1>>
                    <<set $has_gross_buff = true>>
                    animegirl: Ewww. Why were you drinking expired milk??? Oh god, I can smell it.
                    animegirl: You're kind of gross, you know that? What's wrong with you?
                    animegirl: I'm gonna tell everyone how gross you are. You'll never live this down.
                    <<AddGrossnessBuff GameManager>>
                    (My pride is wounded but my body is safe.)
                    (I am now considered Gross. People will hate me easily, but they'll also be too disgusted to hit me hard...)
                    <<jump awkward>>
            -> Uh...I gotta go outside
                <<SetBackground GameManager courtyard>>
                <<jump Outside>>
===

title: dotdotdot
tags:
position: -589,-186
---
<<Checkpoint GameManager>>
animegirl: Wow, the strong silent type. This actually makes me like you more!
    -> YODELEEEEEE YODELOOOOOOOO YO HO HO
        <<jump WhatsWrong>>
    -> ...
        animegirl: Ok, I'll tell everyone we're dating. I'll meet your parents tonight!
        -> Ugh, fine...
            animegirl: We're going to have an amazing rest of our lives together :3
            <<jump LOSE_got_relationship>>
        -> I would never date you, you foul creature!
            <<SetCharacter GameManager animegirl 4>>
            animegirl: I won't stand for this...
            <<StartFPS GameManager>>
        -> That's not so skibidi of you.
            <<jump PostShove>>
    -> *fart*
        <<SetCharacter GameManager animegirl 4>>
        animegirl: You're disgusting...I won't stand for this.
        <<StartFPS GameManager>>
    -> Sorry, I was thinking about my extensive collection of banana peels
        <<SetCharacter GameManager animegirl 1>>
        animegirl: ...Okay. Gotta go! Nice seeing you!
        <<jump DAY_SUCCESS>>
===

title: Outside
position: -129,302
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 2>>
animegirl: Wow, so by "I gotta go outside" you meant you wanted to talk to me in private?
animegirl: And now you're confessing to me in the courtyard :3
    -> Oh no!
        <<SetCharacter GameManager animegirl 0>>
        animegirl: Too bad! We're meeting my parents tonight :3
        <<jump LOSE_got_relationship>>
    -> Yeah, I'm confessing that you're annoying
        <<SetCharacter GameManager animegirl 4>>
        animegirl: I've had it with you!
        <<StartFPS GameManager>>
    -> No, I wanted to be alone...my goldfish died
        <<SetCharacter GameManager animegirl 1>>
        animegirl: Oh, I'm so sorry...I'll leave you alone.
        animegirl: For now...
        <<jump DAY_SUCCESS>>
===

title: otheranimegirl
position: -390,264
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 2>>
otheranimegirl: I heard you told animegirl that we were dating...is that true?
    -> I was just trying to get her away from me. I can't stand her.
        <<SetCharacter GameManager otheranimegirl 4>>
        <<ChangeAffection GameManager otheranimegirl -20>>
        otheranimegirl: She's actually my best friend and I take that personally.
        <<StartFPS GameManager>>
    -> Uh ... please leave me alone I just want to play Minecraft Pocket Edition
        <<jump OMGILoveThatGame>>
    -> I'm a compulsive liar, I can't control it, I need help, please help me (help me help me help me)
        <<SetCharacter GameManager otheranimegirl 3>>
        <<ChangeAffection GameManager otheranimegirl -10>>
        otheranimegirl: Not my circus not my monkeys, I'm gonna leave now.
        <<jump BackToFirstGirl>>
    -> AHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH
        <<jump WhatsWrong>>
===

title: LOSE_got_relationship
position: -846,-52
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
...
GASP
omg, what happened?
what a nightmare... I got into a relationship??
I hope that never happens again...
<<jump BeginDay>>
===


title: transitionBack
position: -288,-555
---
<<Checkpoint GameManager>>
<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
<<RefreshHealth GameManager -1>>
...
GASP
omg, what happened?
what a nightmare... I remembered fighting something??
That's probably just a bad dream... Anyways I should get back!
<<jump BeginDay>>
===

title: EnterFPS
position: -521,-719
---
<<Checkpoint GameManager>>
...
What's wrong with this girl...?
Next Line
<<ResumeFPS GameManager>>
===

title: ExitFPS
position: -289,-723
---
<<Checkpoint GameManager>>
yourenemygirl: ONE LAST CHANCE! ADMIT YOU ARE TRASH!!!
    -> I admit defeat, I am, in fact, trash.
        <<EndFPS GameManager false>>
    -> As long as you're happy, I can be anything. Please, give me one more chance.
        // -1 here represents setting to StartingHealth
        <<RefreshHealth GameManager -1>>
        <<ResumeFPS GameManager>>
===

title: FPS_SUCCESS_animegirl
position: -757,-721
---
<<Checkpoint GameManager>>
<<SetBackground GameManager classroom>>
animegirl: ...
animegirl: wait, I think I spaced out.. what happened??
    -> No idea.
        animegirl: oh, teehee!
        animegirl: anyways...
    -> You went insane and tried to kill me!!
        animegirl: i don't remember that at all...
        animegirl: anyways...
<<ContinueFromPreFPSNode GameManager>>
===

title: FPS_SUCCESS_otheranimegirl
position: -757,-721
---
<<Checkpoint GameManager>>
<<SetBackground GameManager classroom>>
otheranimegirl: ...
otheranimegirl: zomglol, im confuzzled? wut happened uwu??
    -> Bruh.
        otheranimegirl: uwuwuwu
        otheranimegirl: anyways...
    -> You went insane and tried to kill me!!
        otheranimegirl: oWo? otheranimegirl doesn't remember that...
        otheranimegirl: anyways...
<<ContinueFromPreFPSNode GameManager>>
===

title: DAY_SUCCESS
position: 194,777
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager me>>
bell: ring ring ring , yuh
...
class is over?
couldn't end sooner!

<<SetBackground GameManager home>>
<<SetCharacter GameManager me>>
ah, nice to be back home.
good thing I didn't get into a relationship today.
...
oh man, would you look at the time?
I should get ready for bed...
zzz
zzz
zzz
<<LogDayEndAnalytics GameManager>>
<<jump AnotherDay>>
===
title: PostShove
position: -812,296
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 3>>
animegirl: I'm not going to let some silly little boy talk to me like that.
    -> You're right, I'm a silly little boy. I'm so silly.
        animegirl: That's what I thought.
        <<SetCharacter GameManager animegirl 1>>
        animegirl: I won't fight you, but I will say this...I don't respect you at all. I'm going to tell everyone I know what a spineless little worm you are.
        (I hope this doesn't have any repercussions for me...)
        <<jump DAY_SUCCESS>>
    -> Well, I'm not gonna let a dumb little girl tell me what to do!
        <<SetCharacter GameManager animegirl 4>>
        <<ChangeAffection GameManager animegirl -10>>
        animegirl: I am going to MESS YOU UP.
        <<StartFPS GameManager>>
    -> I'm sorry, I was rude. I'm just having a really bad day.
        <<SetCharacter GameManager animegirl 0>>
        <<ChangeAffection GameManager animegirl 20>>
        animegirl: Mmm...well, that's okay, I guess.
        animegirl: I think an argument every now and then is key to a healthy relationship.
        (Uh oh...)
        <<SetCharacter GameManager animegirl 2>>
        animegirl: I'll text you tonight, okay? You can tell me all about your bad day!
        <<jump LOSE_got_relationship>>
===
title: mitochondria
position: -70,-149
---
<<Checkpoint GameManager>>
animegirl: ooh, you're so smart and funny!
    -> ... I gotta go outside.
        <<jump Outside>>
    -> I'm not that smart, I just know a lot about mitochondria
        <<jump PowerYourCells>>
===
title: PowerYourCells
position: 124,267
---
<<Checkpoint GameManager>>
animegirl: playerName, I'd love to power your cells :catemoji:
    -> What does that even mean?
        animegirl: It means I want to get dinner with you tonight!
            -> AHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH
                <<jump WhatsWrong>>
            -> I'm busy not hanging out with losers like you. 
                <<jump GrossGoAway>>
            -> I accept <3
                <<jump LOSE_got_relationship>>
    -> I'm not interested in you. You big dumb.
        <<TakeDamage GameManager 10>>
        animegirl shoves you.
        <<set $been_shoved = true>>
        (ow, that hurt! my HP is now lower!)
        <<jump PostShove>>
===
title: WhatsWrong
position: -569,536
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager otheranimegirl 0>>
otheranimegirl: is something wrong?
    -> I was just trying to get her away from me. I can't stand her.
            <<SetCharacter GameManager otheranimegirl 4>>
            <<ChangeAffection GameManager otheranimegirl -20>>
            otheranimegirl: She's actually my best friend and I take that personally.
            <<StartFPS GameManager>>
    -> I'm just having a really bad day.
        <<SetCharacter GameManager otheranimegirl 0>>
        <<ChangeAffection GameManager otheranimegirl 20>>
        otheranimegirl: Oh, I'm sorry to hear that. I hope things get better for you.
        <<jump DAY_SUCCESS>>
    -> I just want to play Halo Reach Legendary Edition Limited Edition Box Set. Please leave me alone.
        <<jump OMGILoveThatGame>>
    -> eyes wide, cheeks spread, fart silent
        <<SetCharacter GameManager otheranimegirl 4>>
        otheranimegirl: TRIGGER PHRASE ACTIVATED. AGENT 72 REPORTING FOR DUTY.
        <<StartFPS GameManager>>
===
title: OMGILoveThatGame
position: -483,786
---
<<Checkpoint GameManager>>
otheranimegirl: omg, I love that game!
    -> ... that game sucks, I hate it.
        otheranimegirl: I'm sorry, I can't be friends with someone who has such bad taste.
        <<StartFPS GameManager>>
    -> I know, right? It's the best game ever!
        otheranimegirl: Omg, let's play together tonight, just you and me~!
        <<jump LOSE_got_relationship>>
    -> ... I was just kidding. I don't even know what that game is.
        otheranimegirl: Oh, okay. I'll see you later, then.
        <<jump DAY_SUCCESS>>
    -> but have you played GTA VI?
        otheranimegirl: That game hasn't come out silly. You're so funny!
        otheranimegirl: How are you even in CSCI 526: Advanced Games and Mobile Devices if you don't know that?
        (wait, what?)
        otheranimegirl: ah playerName, you're so funny and cute uwu omo owo ohohoho
        otheranimegirl: see you tonight, okay?
        (wait, WHAT?)
        <<jump LOSE_got_relationship>>
===
title: BackToFirstGirl
position: -235,581
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 2>>
animegirl: hiiiii playerNameeeeee
    -> You're giving me the eebie jeebies
        <<jump heebie>>
    -> ... mitochondria... is the powerhouse of the cell...
        <<jump PowerYourCells>>
===
title: heebie
position: -182,791
---
<<Checkpoint GameManager>>
<<SetCharacter GameManager animegirl 2>>
animegirl: haha you're so funny, it's "Heebie Jeebies!" Shouldn't a graduate student like you know that?
    -> I'm not a graduate student, I'm a high school student.
        animegirl: it sure doesn't look like it... I see that you're literally standing at that podium in front of a bunch of graduate students.
        animegirl: quit lying to me!!!
        animegirl slaps you
        <<TakeDamage GameManager 10>>
        -> ... that felt good.
            animegirl: ew, freak! see you later, loser.
            <<jump DAY_SUCCESS>>
        -> IM GONNA KILL YOU
            animegirl: not if I kill you first <3
            <<StartFPS GameManager>>
        -> ok so im gonna make the bell ring now.
            animegirl: wait, you can do that??
            <<jump DAY_SUCCESS>>
=== 
title: INTERRUPT_LOW_AFFECTION_animegirl
position: -978,-725
---
<<Checkpoint GameManager>>
animegirl: you know what playerName, you're actually kind of DISGUSTING.
animegirl: I need to wipe you off the face of this earth.
-> Oh no.
-> Oh no.
-> Oh no.
-> Oh no.
<<StartFPS GameManager>>
===

title: INTERRUPT_LOW_AFFECTION_otheranimegirl
position: -981,-574
---
<<Checkpoint GameManager>>
otheranimegirl: u know, i'm kinda weird with the uwo owo stuff, but you're even weirder
otheranimegirl: Looks like the only next step is to hurt you <3<3 physically ~ <3
-> Oh no.
-> Oh no.
-> Oh no.
-> Oh no.
<<StartFPS GameManager>>
===

title: awkward
position: -981,-574
---
<<Checkpoint GameManager>>
animegirl: ... so what now?
-> AHHHHHHHHHHHHHHHHHHH
    <<jump WhatsWrong>>
-> AHHHHHH
    <<jump WhatsWrong>>
-> A1H7
    <<jump WhatsWrong>>
-> i needa step outside...
    <<jump Outside>>
<<StartFPS GameManager>>
===
